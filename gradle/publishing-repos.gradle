/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later
 * See the lgpl.txt file in the root directory or http://www.gnu.org/licenses/lgpl-2.1.html
 */

apply from: rootProject.file( 'gradle/base-information.gradle' )


apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'
apply plugin: 'com.jfrog.bintray'


ext {
	bintrayUser = project.hasProperty( 'PERSONAL_BINTRAY_USER' ) ? project.property( 'PERSONAL_BINTRAY_USER' ) : null
	bintrayKey = project.hasProperty( 'PERSONAL_BINTRAY_API_KEY' ) ? project.property( 'PERSONAL_BINTRAY_API_KEY' ) : null
}


publishing {
	publications {
		publishedArtifacts( MavenPublication )
	}

	repositories {
		if ( System.getProperty('accessToken') != null ) {
			maven {
				name 'maven-repository'
				url System.getProperty('maven.deploy.url')
				credentials(HttpHeaderCredentials) {
					name = "Authorization"
					value = "Bearer " + System.getProperty('accessToken')
				}
				authentication {
					header(HttpHeaderAuthentication)
				}
			}
		}
		else {
			maven {
				name 'maven-repository'
				url System.getProperty('maven.deploy.url')
			}
		}
	}
}

bintray {
	user = project.bintrayUser
	key = project.bintrayKey

	publications = ['publishedArtifacts']

	pkg {
		userOrg = 'hibernate'
		repo = 'artifacts'
		name = 'hibernate-orm'

		publish = true

		version {
			name = project.version
			released  = new Date()
			vcsTag = project.version
			gpg {
				sign = true
			}
			attributes = [
					'jpa': project.jpaVersion,
					'family': project.ormVersion.family
			]
			mavenCentralSync {
				sync = true
			}
		}
	}
}

model {
	tasks.generatePomFileForPublishedArtifactsPublication {
		destination = file( "${buildDir}/generated-pom.xml" )
	}
}

task generatePomFile( dependsOn: 'generatePomFileForPublishedArtifactsPublication' )
